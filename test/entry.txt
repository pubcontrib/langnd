#!/usr/local/bin/langnd

# entry.txt: test for expected behavior from a langnd program build
#
# Usage:
#     entry.txt

$suite =
<
  # run a suite of tests

  $name = argument

  import ["read", "evaluate", "merge"] from core

  $file = $merge($name, ".txt")
  $file = $merge("suite/", $file)
  $thrown = catch $evaluate($read($file, null))

  import ["equals", "not", "freeze"] from core

  if $not($equals($thrown, null))
  \
    import ["evaluate", "read", "get", "write"] from core

    $testing = $evaluate($read("testing.txt", null))
    $writeDetail = $get($testing, "writeDetail")
    $writeStatus = $get($testing, "writeStatus")
    $color = $get($testing, "color")

    $writeStatus("FAIL", $color, 31)

    import ["get", "thaw", "type", "equals"] from core

    $issue = $get($thrown, "issue")
    $cause = null
    catch $cause = $get($thrown, "cause")

    if $equals($type($cause), "MAP")
    \
      $writeDetail("Reason", $get($cause, "reason"), $color)
      $writeDetail("Suite", $name, $color)
      $writeDetail("Source", $get($cause, "source"), $color)

      catch $writeDetail("Actual", $freeze($get($cause, "actual")), $color)
      catch $writeDetail("Expected", $freeze($get($cause, "expected")), $color)
      catch $writeDetail("Returned", $freeze($get($cause, "returned")), $color)
      catch $writeDetail("Thrown", $freeze($get($cause, "thrown")), $color)
    /
    otherwise
    \
      $writeDetail("Reason", $issue, $color)
      $writeDetail("Suite", $name, $color)

      catch $writeDetail("Cause", $freeze($get($thrown, "cause")), $color)
      catch $writeDetail("Hint", $freeze($get($thrown, "hint")), $color)
    /

    $write("\n", 2)

    throw "test suite failed"
  /
>

$suite("token/unknown")
$suite("token/whitespace")
$suite("token/string")
$suite("token/identifier")

$suite("expression/null")
$suite("expression/boolean")
$suite("expression/number")
$suite("expression/string")
$suite("expression/list")
$suite("expression/map")
$suite("expression/function")
$suite("expression/assignment")
$suite("expression/invoke")
$suite("expression/branch")
$suite("expression/loop")
$suite("expression/catch")
$suite("expression/argument")
$suite("expression/return")
$suite("expression/break")
$suite("expression/continue")
$suite("expression/throw")
$suite("expression/import")
$suite("expression/snippet")

$suite("core/add")
$suite("core/subtract")
$suite("core/multiply")
$suite("core/divide")
$suite("core/modulo")
$suite("core/truncate")
$suite("core/and")
$suite("core/or")
$suite("core/not")
$suite("core/precedes")
$suite("core/succeeds")
$suite("core/equals")
$suite("core/write")
$suite("core/read")
$suite("core/delete")
$suite("core/query")
$suite("core/evaluate")
$suite("core/freeze")
$suite("core/thaw")
$suite("core/type")
$suite("core/cast")
$suite("core/get")
$suite("core/set")
$suite("core/unset")
$suite("core/merge")
$suite("core/length")
$suite("core/keys")
$suite("core/sort")
$suite("core/inputs")

# any tester making it this far passes
import ["evaluate", "read", "get", "write"] from core

$testing = $evaluate($read("testing.txt", null))
$writeStatus = $get($testing, "writeStatus")
$color = $get($testing, "color")

$writeStatus("PASS", $color, 32)
